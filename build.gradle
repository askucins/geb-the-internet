// Gretty https://github.com/gretty-gradle-plugin/gretty
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.gretty:gretty:+'
    }
}

plugins {
    id 'idea'
    id 'groovy'
    id 'java-library'
    id 'com.github.erdi.webdriver-binaries' version '2.2'
    id 'com.github.erdi.idea-base' version '2.2'
}

apply plugin: 'org.gretty'

wrapper {
    gradleVersion = '6.2.2'
    distributionType = Wrapper.DistributionType.ALL
}

ext {
    // GebConfig environments
    drivers = ['firefox', 'firefoxHeadless', 'chrome', 'chromeHeadless']
    defaultDriver = 'chrome'
    ext {
        gebVersion = '3.3'
        webdriverVersion = '4.0.0-alpha-4'
        chromeDriverVersion = '80.0.3987.106' //16
        geckoDriverVersion = '0.26.0'
    }
}

webdriverBinaries {
    chromedriver {
        version = chromeDriverVersion
        fallbackTo32Bit = false
    }
    geckodriver geckoDriverVersion
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:2.5.10'
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5', {
        exclude module: 'groovy-all'
    }
    implementation 'org.slf4j:slf4j-api:2.0.0-alpha1'
    implementation 'ch.qos.logback:logback-classic:1.3.0-alpha5'
    implementation "org.gebish:geb-core:${gebVersion}"
    testImplementation "org.gebish:geb-spock:${gebVersion}"
    implementation "org.seleniumhq.selenium:selenium-chrome-driver:${webdriverVersion}"
    implementation "org.seleniumhq.selenium:selenium-firefox-driver:${webdriverVersion}"
    implementation "org.seleniumhq.selenium:selenium-support:${webdriverVersion}"
    testImplementation('com.athaydes:spock-reports:1.7.1') {
        transitive = false // this avoids affecting your version of Groovy/Spock
    }

    // Proxy https://github.com/browserup/browserup-proxy
    def versionBrowserUp = '2.0.1'
    implementation "com.browserup:browserup-proxy-core:${versionBrowserUp}"
    implementation "com.browserup:browserup-proxy-mitm:${versionBrowserUp}"
    // Proxy https://github.com/mrog/LittleProxy
    implementation 'xyz.rogfam:littleproxy:2.0.0-beta-5'

    // OCR - Tesseract
    // Tesseract requires to install the library, e.g
    // [root@nighthawk tmp]# rpm -q tesseract
    // tesseract-3.04.00-3.el7.x86_64
    // TODO add verification of the installed binary
    implementation 'net.sourceforge.tess4j:tess4j:4.5.1'
}

sourceSets {
    integration {
        groovy.srcDir "$projectDir/src/integration/groovy"
        resources.srcDir "$projectDir/src/integration/resources"
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integrationImplementation.extendsFrom testImplementation
    integrationRuntime.extendsFrom testRuntime
}

tasks.withType(Test) {
    systemProperty 'geb.env', project.findProperty('webdriver') ?: defaultDriver
    systemProperty 'geb.build.reportsDir', 'reports'
    systemProperty 'geb.build.baseUrl', 'http://the-internet.herokuapp.com/'
    systemProperty 'org.askucins.tesseract', '/usr/share/tesseract'

    testLogging {
        showStandardStreams = true
        events 'passed', 'failed'
        exceptionFormat 'full'

        debug {
            events 'passed', 'skipped', 'failed'
            showStackTraces true
            exceptionFormat 'full'
        }
    }
}

String localContextPath = '/gebbook'

task integrationTest(type: Test) {
    group = 'verification'
    description = 'runs integration tests via gretty'
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath

    systemProperty 'geb.build.baseUrl', "http://localhost:8080${localContextPath}/"
}

check.dependsOn integrationTest

gretty {
    contextPath = localContextPath
    integrationTestTask = 'integrationTest'
}
