//TODO Fix these dummy workarounds for Intellij
buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'org.apache.ivy:ivy:2.5.2'
        classpath 'org.apache.groovy:groovy-json:4.0.24'
    }
}

import groovy.json.JsonBuilder
import groovy.json.JsonOutput
import groovy.json.JsonSlurper
import org.gradle.jvm.toolchain.JavaLanguageVersion

/**
 * Properties (not mandatory):
 * qa.webdriver - webdriver name; one of: 'firefoxBare', 'firefox', 'firefoxHeadless', 'chromeBare', 'chrome', 'chromeHeadless'
 * qa.chromium - if present non-standard chromedriver version will be used (only POC)
 */

buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        // Gretty https://github.com/gretty-gradle-plugin/gretty
        classpath 'org.gretty:gretty:+'
    }
}

plugins {
    id 'idea'
    id 'groovy'
    id 'java'
    id 'java-library'
    id 'com.github.erdi.webdriver-binaries' version '3.2'
    id 'com.github.erdi.idea-base' version '2.2'
    id 'org.asciidoctor.jvm.convert' version '4.0.0-alpha.1'
    id 'org.gretty' version '4.1.0'
}

/**
 * Wrapper
 */

wrapper {
    //Gotcha! Version 8.7 fails with com.github.erdi.webdriver-binaries
    //TODO monitor https://github.com/erdi/webdriver-binaries-gradle-plugin/issues/43
    gradleVersion = '8.6'
    distributionType = Wrapper.DistributionType.ALL
}

/**
 * Geb and Webdriver mgmt
 * Properties:
 *  qa.webdriver
 *  qa.chromium
 */

tasks.withType(Test) {
    systemProperty 'geb.env', project.findProperty('qa.webdriver')
    systemProperty 'geb.build.reportsDir', 'reports'
}

webdriverBinaries {
    driverUrlsConfiguration = resources.text.fromFile('webdriver.json')
    chromedriver {
        version = {
            (project.findProperty('qa.chromium') != null)
                ? '130.0.6723.69'
                : '131.0.6778.108'
        }()
        fallbackTo32Bit = true
    }
    geckodriver '0.33.0'
}

task refreshWebdriverConfiguration {
    group = 'other'
    description = 'refreshes configuration of webdriver binaries'

    def slurper = new JsonSlurper()
    def customWebdriver = slurper.parseText(resources.text.fromFile('webdriver-custom.json').asFile().text)
    try {
        def remoteWebdriver = slurper.parseText(new URL('https://raw.githubusercontent.com/webdriverextensions/webdriverextensions-maven-plugin-repository/master/repository-3.0.json').text)
        def finalWebdriver = slurper.parseText(new JsonBuilder([drivers: customWebdriver.drivers + remoteWebdriver.drivers]).toString())
        def webdriverJson = new File(projectDir, 'webdriver.json')
        webdriverJson.delete()
        webdriverJson << JsonOutput.prettyPrint(JsonOutput.toJson(finalWebdriver))
    } catch (e) {
        logger.error("Unable to refresh webdriver versions configuration!")
        logger.error(e.asString())
    }
}

task removeWebdriverInstances() {
    group = 'other'
    description = 'removes hanging instances of webdriver'

    def doRemoveWebdriverInstances = {
        exec {
            ignoreExitValue true
            commandLine "pgrep", "-f", "webdriver"
        }
        exec {
            ignoreExitValue true
            commandLine "pgrep", "-f", "chromedriver"
        }
        exec {
            ignoreExitValue true
            commandLine "pgrep", "-f", "marionette"
        }
        exec {
            ignoreExitValue true
            commandLine "pkill", "-f", "webdriver"
        }
        exec {
            ignoreExitValue true
            commandLine "pkill", "-f", "chromedriver"
        }
        exec {
            ignoreExitValue true
            commandLine "pkill", "-f", "marionette"
        }
    }

    doLast {
        doRemoveWebdriverInstances()
    }
}

task removeJettyInstances() {
    group = 'other'
    description = 'removes hanging instances of jetty'

    def doRemoveJettyInstances = {
        exec {
            ignoreExitValue true
            commandLine "pgrep", "-f", "jetty"
        }
        exec {
            ignoreExitValue true
            commandLine "pkill", "-f", "jetty"
        }
    }

    doLast {
        doRemoveJettyInstances()
    }
}

task removeTestResults(type: Delete) {
    group = 'other'
    description = 'removes geb reports and logs'
    delete "reports"
    delete "logs"
}

/**
 * Integration tests with gretty
 */

sourceSets {
    integration {
        groovy.srcDir "$projectDir/src/integration/groovy"
        resources.srcDir "$projectDir/src/integration/resources"
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integrationImplementation.extendsFrom testImplementation
    integrationRuntime.extendsFrom testRuntime
}
// TODO https://discuss.gradle.org/t/gradle-7-fail-for-duplicates-in-copy-specs-has-no-duplicates-in-project/39834/7
tasks.named("processIntegrationResources") {
    duplicatesStrategy = 'include'
}


String localContextPath = '/gebbook'

task integrationTest(type: Test) {
    group = 'verification'
    description = 'runs integration tests via gretty'
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath

    // According to https://gebish.org/manual/current/#base-url
    // Base url with trailing slash, relative urls without leading slash
    systemProperty 'geb.build.baseUrl', "http://localhost:8080${localContextPath}/"
}

gretty {
    contextPath = localContextPath
    integrationTestTask = 'integrationTest'
}

check.dependsOn integrationTest

/**
 *  Usual configuration
 */

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.groovy:groovy-all:4.0.24'
    implementation 'org.apache.groovy:groovy-json:4.0.24'
    implementation 'org.apache.groovy:groovy-yaml:4.0.24'
    implementation 'org.apache.groovy:groovy-xml:4.0.24'
    implementation 'org.apache.groovy:groovy-ginq:4.0.24'
    implementation 'org.slf4j:slf4j-api:2.0.16'
    implementation 'ch.qos.logback:logback-classic:1.5.7'
    def spockVersion = '2.4-M4-groovy-4.0'
    implementation "org.spockframework:spock-core:${spockVersion}", {
        exclude group: 'org.apache.groovy'
    }
    //Gotcha! https://github.com/spockframework/spock/issues/1849
    implementation 'org.junit.platform:junit-platform-testkit:1.11.1'
    def gebVersion = '7.0'
    implementation "org.gebish:geb-core:${gebVersion}"
    testImplementation "org.gebish:geb-spock:${gebVersion}"
    def webdriverVersion = '4.27.0'
    implementation 'io.netty:netty-buffer:4.1.107.Final' // TODO see below - workaround
    implementation("org.seleniumhq.selenium:selenium-support:${webdriverVersion}") {
        exclude group: 'io.netty'
    }
    implementation("org.seleniumhq.selenium:selenium-chrome-driver:${webdriverVersion}") {
        exclude group: 'io.netty'
    }
    implementation("org.seleniumhq.selenium:selenium-chromium-driver:${webdriverVersion}") {
        exclude group: 'io.netty'
    }
    implementation("org.seleniumhq.selenium:selenium-firefox-driver:${webdriverVersion}") {
        exclude group: 'io.netty'
    }
    implementation("org.seleniumhq.selenium:selenium-devtools-v131:${webdriverVersion}") {
        exclude group: 'io.netty'
    }

    testImplementation('com.athaydes:spock-reports:2.5.0-groovy-4.0') {
        transitive = false
    }

    // TODO check https://github.com/AOEpeople/geb-spock-reports
    testImplementation ('com.aoe:geb-spock-reports:0.3.0-RC1') {
        exclude group: 'org.codehaus.groovy'
    }

    implementation('com.assertthat:selenium-shutterbug:1.6') {
        exclude group: 'org.seleniumhq.selenium'
        exclude group: 'org.codehaus.groovy'
    }

    def versionBrowserUp = '3.0.0'
    implementation "com.github.valfirst.browserup-proxy:browserup-proxy:${versionBrowserUp}"
    implementation "com.github.valfirst.browserup-proxy:browserup-proxy-core:${versionBrowserUp}"
    implementation "com.github.valfirst.browserup-proxy:browserup-proxy-mitm:${versionBrowserUp}"

    // Proxy https://github.com/LittleProxy/LittleProxy
    implementation 'io.github.littleproxy:littleproxy:2.3.3'

    // Decompressing Brotli-encoded payload
    implementation 'org.brotli:dec:0.1.2'

    // OCR - Tesseract
    // Tesseract requires to install the library, e.g
    // [root@nighthawk tmp]# rpm -q tesseract
    // tesseract-4.1.1-7.el9.x86_64
    // TODO add verification of the installed binary
    implementation 'net.sourceforge.tess4j:tess4j:5.8.0'

    // Other
    implementation 'org.apache.commons:commons-text:1.12.0'
    implementation 'commons-io:commons-io:2.16.1'
}

tasks.withType(Test) {
    // See https://blog.gradle.org/stop-rerunning-tests
    systemProperty "random.testing.seed", new Random().nextInt()

    systemProperty 'org.askucins.tesseract', '/usr/share/tesseract'

    testLogging {
        showStandardStreams = true
        events 'passed', 'failed'
        exceptionFormat 'full'

        debug {
            events 'passed', 'skipped', 'failed'
            showStackTraces true
            exceptionFormat 'full'
        }
    }
    useJUnitPlatform()
}

/**
 * Misc
 */

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
        //vendor = JvmVendorSpec.ORACLE
    }
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

asciidoctor {
    sourceDir file('.')
    sources {
        include 'readme.adoc'
    }
    outputDir file('build/docs')
}
