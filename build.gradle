import groovy.json.JsonBuilder
import groovy.json.JsonOutput
import groovy.json.JsonSlurper

// Gretty https://github.com/gretty-gradle-plugin/gretty
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'org.gretty:gretty:+'
    }
}

plugins {
    id 'idea'
    id 'groovy'
    id 'java-library'
    id 'com.github.erdi.webdriver-binaries' version '2.2'
    id 'com.github.erdi.idea-base' version '2.2'
}

apply plugin: 'org.gretty'

wrapper {
    gradleVersion = '6.5'
    distributionType = Wrapper.DistributionType.ALL
}

idea {
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

ext {
    // GebConfig environments
    drivers = ['firefox', 'firefoxHeadless', 'chrome', 'chromeHeadless']
    defaultDriver = 'chrome'
    ext {
        gebVersion = '3.4'
        webdriverVersion = '4.0.0-alpha-4' //TODO tests fail with 4.0.0-alpha-5!
        chromeDriverVersion = '83.0.4103.39'
        geckoDriverVersion = '0.26.0'
    }
}

task refreshWebdriverConfiguration {
    group = 'other'
    description = 'refreshes configuration of webdriver binaries'

    def slurper = new JsonSlurper()
    def customWebdriver = slurper.parseText(resources.text.fromFile('webdriver-custom.json').asFile().text)
    def remoteWebdriver = slurper.parseText(new URL('https://raw.githubusercontent.com/webdriverextensions/webdriverextensions-maven-plugin-repository/master/repository-3.0.json').text)
    def finalWebdriver = slurper.parseText(new JsonBuilder([drivers: customWebdriver.drivers + remoteWebdriver.drivers]).toString())

    def webdriverJson = new File(projectDir, 'webdriver.json')
    webdriverJson.delete()
    webdriverJson << JsonOutput.prettyPrint(JsonOutput.toJson(finalWebdriver))
}

webdriverBinaries {
    driverUrlsConfiguration = resources.text.fromFile('webdriver.json')
    chromedriver {
        version = chromeDriverVersion
        fallbackTo32Bit = false
    }
    geckodriver geckoDriverVersion
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:3.0.4'
    testImplementation 'org.spockframework:spock-core:2.0-M2-groovy-3.0', {
        exclude module: 'groovy-all'
    }
    testImplementation 'org.spockframework:spock-junit4:2.0-M2-groovy-3.0' //Temporary workaround for JUnit 4 rules
    implementation 'org.slf4j:slf4j-api:2.0.0-alpha1'
    implementation 'ch.qos.logback:logback-classic:1.3.0-alpha5'
    implementation "org.gebish:geb-core:${gebVersion}"
    testImplementation "org.gebish:geb-spock:${gebVersion}"
    implementation "org.seleniumhq.selenium:selenium-chrome-driver:${webdriverVersion}"
    implementation "org.seleniumhq.selenium:selenium-firefox-driver:${webdriverVersion}"
    implementation "org.seleniumhq.selenium:selenium-support:${webdriverVersion}"
    //TODO Blocked by https://github.com/renatoathaydes/spock-reports/issues/189
    // testImplementation('com.athaydes:spock-reports:1.7.1') {
    //     transitive = false // this avoids affecting your version of Groovy/Spock
    // }

    // Proxy https://github.com/browserup/browserup-proxy
    def versionBrowserUp = '2.1.1'
    implementation "com.browserup:browserup-proxy-core:${versionBrowserUp}"
    implementation "com.browserup:browserup-proxy-mitm:${versionBrowserUp}"
    // Proxy https://github.com/mrog/LittleProxy
    implementation 'xyz.rogfam:littleproxy:2.0.0-beta-5'

    // OCR - Tesseract
    // Tesseract requires to install the library, e.g
    // [root@nighthawk tmp]# rpm -q tesseract
    // tesseract-3.04.00-3.el7.x86_64
    // TODO add verification of the installed binary
    implementation 'net.sourceforge.tess4j:tess4j:4.5.1'

    // Other
    testImplementation 'org.apache.commons:commons-text:1.8'
}

sourceSets {
    integration {
        groovy.srcDir "$projectDir/src/integration/groovy"
        resources.srcDir "$projectDir/src/integration/resources"
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    integrationImplementation.extendsFrom testImplementation
    integrationRuntime.extendsFrom testRuntime
}

task cleanReports(type: Delete) {
    group = 'other'
    description = 'removes geb reports'
    delete 'reports'
}

tasks.withType(Test) {
    systemProperty 'geb.env', project.findProperty('webdriver') ?: defaultDriver
    systemProperty 'geb.build.reportsDir', 'reports'
    systemProperty 'org.askucins.tesseract', '/usr/share/tesseract'

    testLogging {
        showStandardStreams = true
        events 'passed', 'failed'
        exceptionFormat 'full'

        debug {
            events 'passed', 'skipped', 'failed'
            showStackTraces true
            exceptionFormat 'full'
        }
    }
    useJUnitPlatform()
}

String localContextPath = '/gebbook'

task integrationTest(type: Test) {
    group = 'verification'
    description = 'runs integration tests via gretty'
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath

    systemProperty 'geb.build.baseUrl', "http://localhost:8080${localContextPath}/"
}

check.dependsOn integrationTest

gretty {
    contextPath = localContextPath
    integrationTestTask = 'integrationTest'
}

task removeChromedriverInstances(type: Exec) {
    ignoreExitValue true

    commandLine "pgrep", "-f", "webdriver"
    commandLine "pgrep", "-f", "chromedriver"
    commandLine "pkill", "-f", "webdriver"
    commandLine "pkill", "-f", "chromedriver"
}
